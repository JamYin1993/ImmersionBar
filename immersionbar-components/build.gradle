apply plugin: 'com.android.library'
//apply plugin: 'com.novoda.bintray-release'

android {
    compileSdkVersion 28
    buildToolsVersion '28.0.3'

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 28
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

}

dependencies {
    compileOnly 'androidx.appcompat:appcompat:1.0.2'
    compileOnly project(':immersionbar')
}
//
//publish {
//    userOrg = 'yin'
//    groupId = 'com.yyj.immersionbar'
//    artifactId = 'immersionbar-components'
//    publishVersion = "$immersionbar_version"
//    desc = 'Android bar management'
//    website = 'https://github.com/JamYin1993/ImmersionBar'
//}



//需添加如下内容

apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'


//发布到组织名称名字，必须填写
group = "com.yyj.immersionbar"
//发布到JCenter上的项目名字，必须填写
def libName = "immersionbar-components"
// 版本号，下次更新是只需要更改版本号即可
version = "$immersionbar_version"
/**  上面配置后上传至jcenter后的编译路径是这样的： compile       'com.fighter.maven:mylibs:1.0.0'  **/

//生成源文件
task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}
//生成文档
task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    options.encoding "UTF-8"
    options.charSet 'UTF-8'
    options.author true
    options.version true
    failOnError false
}

//文档打包成jar
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}
//拷贝javadoc文件
task copyDoc(type: Copy) {
    from "${buildDir}/docs/"
    into "docs"
}

//上传到jcenter所需要的源码文件
artifacts {
    archives javadocJar
    archives sourcesJar
}

// 配置maven库，生成POM.xml文件
install {
    repositories.mavenInstaller {
        // This generates POM.xml with proper parameters
        pom {
            project {
                packaging 'aar'
                name 'Android bar management'
                developers {
                    developer {
                        id 'yin'
                        name 'Jam Yin'
                        email '737338920@qq.com'
                    }
                }
            }
        }
    }
}

//上传到jcenter
Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
bintray {
    user = properties.getProperty("bintray.user")    //读取 local.properties 文件里面的 bintray.user
    key = properties.getProperty("bintray.apikey")   //读取 local.properties 文件里面的 bintray.apikey
    configurations = ['archives']
    pkg {
        userOrg = "jamyin"
        repo = "maven"
        name = libName    //发布到JCenter上的项目名字，必须填写
        desc = 'Android bar management'    //项目描述
        licenses = ["Apache-2.0"]
        publish = true
    }
}

